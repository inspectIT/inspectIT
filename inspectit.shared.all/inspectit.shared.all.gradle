/**
 * Gradle build file for the inspectit.shared.all project.
 * 
 * @author Rene Kugel
 * @author Ivan Senic
 */
defaultTasks 'releaseAndAnalyze'

/** used by the eclipse buildship plugin */
sourceCompatibility = '1.5'
targetCompatibility = '1.5'

/** Artifact names */
ext {
	distJarName = 'inspectit-shared-all'
	pluginName = 'rocks.inspectit.shared.all'
}

/** Setting compile configuration as plugin in Eclipse needs it */
configurations {
	compile {
		extendsFrom configurations.sharedAllProd
	}
	testCompile {
		extendsFrom configurations.sharedAllTest
	}
	testArchives.extendsFrom (testCompile)
}

/** Compile compatibility to 1.5 for all compile tasks */
tasks.withType(JavaCompile) { 
	sourceCompatibility = '1.5'
	targetCompatibility = '1.5'

	options.bootClasspath = configurations.java15rt.asPath
}

/** 
 * Creates the jar. If needed create version.log on the fly and update the version in the MANIFEST.MF.
 */
jar {
	archivesBaseName = distJarName
	
	// add version.log only to shared.all
	if (project.hasProperty('versionInfo')) {
		def tempFile = File.createTempFile('version', '.log')
		tempFile.text = versionInfo
		tempFile.deleteOnExit()
		
		into('') {
			from tempFile
			rename tempFile.getName(), 'version.log'
		}
	}
	
	manifest  {
		from ("${mainResources}/META-INF/MANIFEST.MF") {
			eachEntry { details ->
				if (details.key == 'Bundle-Version' && project.hasProperty('versionInfo')) {
					details.value =  versionInfo
				}
			}
		}
	}
}

task releaseAndAnalyze {
	description = "Runs all unit tests, all checks and assembles a jar archive."
	group = 'Release'

	dependsOn(analyze, jar)
}

/**
 * The following task is needed because we want to have our base test classes available in the other sub-projects, too.
 */
task ('jarTest', type:Jar, dependsOn: testClasses, description: 'Assembles a jar archive from the test source set') {
	group = BasePlugin.BUILD_GROUP
	from project.sourceSets.test.output
	include '**/testbase/*'
	classifier = 'test'
}

/**
 * The artifact definition for this new test jar defined by the jarTest task.
 */
artifacts {
	testArchives project.jarTest
}
