/**
 * Gradle build file for the inspectit.server.diagnosis project.
 * 
 * @author Alexander Wert
 */
evaluationDependsOn(':inspectit.shared.all')
defaultTasks 'releaseAndAnalyze'

/** used by the eclipse buildship plugin */
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

/** Artifact names */
ext {
	distJarName = 'inspectit-server-diagnosis'
}

/** Setting compile configuration as plugin in Eclipse needs it */
configurations {
	compile {
		extendsFrom configurations.serverDiagnosisProd
	}
	testCompile {
		extendsFrom configurations.serverDiagnosisTest
		extendsFrom configurations.jmhbase
	}
}

/** testCompile must depend on shared all test sources because of TestBase class */
dependencies {
	compile project(':inspectit.shared.all')
	compile project(':inspectit.shared.cs')
	testCompile project (path: ':inspectit.shared.all', configuration: 'testArchives')
}

/** Compile compatibility to 1.8 for all compile tasks */
tasks.withType(JavaCompile) { 
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
}

/**
 * Creates the jar. If needed update the version in the MANIFEST.MF.
 */
jar {
	archivesBaseName = distJarName
}

task releaseAndAnalyze {
	description = "Runs all unit tests, all checks and assembles a jar archive."
	group = 'Release'

	dependsOn(analyze, jar)
}
